buildscript {
    ext {
        springBootVersion = '2.0.5.RELEASE'
        swaggerVersion = '2.7.0'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}

plugins {
    id "org.sonarqube" version "2.6.2"
    id 'jacoco'
}

sonarqube {
    properties {
        property 'sonar.host.url', 'https://sonarcloud.io'
        property 'sonar.organization', 'blue-bash'
    }
}

jacoco {
    toolVersion = "0.8.2"
    reportsDir = file("$buildDir/customJacocoReportDir")
}


allprojects {
    apply plugin: 'idea'
    apply plugin: 'eclipse'
    apply plugin: 'jacoco'
    apply plugin: 'java'
}

// Instructions for each sub project
subprojects {
    apply plugin: 'io.spring.dependency-management'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    repositories {
        mavenCentral()
    }
}

project(':common') {
    eclipse {
        project {
            name = 'UrlShortener.common'
            swaggerVersion = '2.7.0'
        }
    }

    dependencyManagement {
        imports {
            mavenBom "org.springframework.boot:spring-boot-dependencies:$springBootVersion"
        }
    }

    dependencies {
        // https://mvnrepository.com/artifact/io.springfox/springfox-swagger2
        compile group: 'io.springfox', name: 'springfox-swagger2', version: '2.7.0'
        // https://mvnrepository.com/artifact/io.springfox/springfox-swagger-ui
        compile group: 'io.springfox', name: 'springfox-swagger-ui', version: '2.7.0'
        // https://mvnrepository.com/artifact/com.github.joschi.jackson/jackson-datatype-threetenbp
        compile group: 'com.github.joschi.jackson', name: 'jackson-datatype-threetenbp', version: '2.6.4'
        compile 'org.hibernate:hibernate-core:5.3.7.Final'
        // https://mvnrepository.com/artifact/com.zaxxer/HikariCP
        compile group: 'com.zaxxer', name: 'HikariCP', version: '3.2.0'

        // Required for QR generation
        compile "com.google.zxing:core:3.3.0"
        compile 'com.google.zxing:javase:3.3.0'

        compile 'org.springframework.boot:spring-boot-starter-web'
        compile 'org.springframework.boot:spring-boot-starter-jdbc'
        compile 'org.springframework.hateoas:spring-hateoas'

        compile 'commons-validator:commons-validator:1.6'
        compile 'com.google.guava:guava:23.0'

        testCompile 'org.springframework.boot:spring-boot-starter-test'
        testRuntime 'org.hsqldb:hsqldb'
    }
}

project(':demo') {
    eclipse {
        project {
            name = 'UrlShortener.demo'
        }
    }

    apply plugin: 'org.springframework.boot'

    dependencies {
        compile project(':common')

        runtime 'org.hsqldb:hsqldb'

        compile 'org.webjars:bootstrap:3.3.5'
        compile 'org.webjars:jquery:2.1.4'

        // Testing framework
        testCompile 'org.springframework.boot:spring-boot-starter-test'
        testCompile 'org.apache.httpcomponents:httpclient'
    }

    jacocoTestReport {
        reports {
            xml.enabled false
            csv.enabled false
        }
    }
}

project ('team') {
    eclipse {
        project {
            name = 'UrlShortener.team'
        }
    }

    apply plugin: 'org.springframework.boot'

    dependencies {
        compile project(':common')
    }
}
